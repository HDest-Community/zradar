version "4.6"

enum RMode
{
	Mode_Disabled,
	Mode_Full,
	Mode_Local
}

class RadarBlip
{
	enum BlipColors
	{
		Col_Red = 0xFFFF2222,
		Col_Orange = 0xFFFF7711,
		Col_Gold = 0xFFFFDD00,
		Col_Green = 0xFF11FF11,
		Col_BlueGreen = 0xFF55FF77,
		Col_Cyan = 0xFF11FFFF,
		Col_Blue = 0xFF3366FF,
		Col_Purple = 0xFF7755FF,
		Col_Pink = 0xFFFF55FF,
		Col_Tan = 0XFFDBC9A4,
		Col_DarkPurple = 0xFF310070
	}

	Actor Other;
	vector3 Diff;
	vector2 DrawPos;
	string Icon, IconBelow, IconAbove;
	Color DrawCol;
	vector2 DrawBox;
	bool IsHostile;
	bool Scanned;
	double Alpha;

	static RadarBlip Create(PlayerPawn plr, Actor other, string iconNormal, Color drawCol, string iconBelow = "", string iconAbove = "", vector2 drawBox = (5, 5))
	{
		let blip = new('RadarBlip');

		blip.Alpha = 1.0;
		blip.Other = other;
		blip.DrawCol = drawCol;
		blip.DrawBox = drawBox;
		blip.IsHostile = other.bISMONSTER && !other.bFRIENDLY;
		blip.Icon = iconNormal;
		blip.IconBelow = iconBelow;
		blip.IconAbove = iconAbove;

		return blip;
	}

	ui string GetIcon()
	{
		return Diff.z <= -128 && IconBelow != "" ? IconBelow : (Diff.z >= 128 && IconAbove != "" ? IconAbove : Icon);
	}
}

class ZRadarMainHandler : EventHandler
{
	// ----------------------------------------------------------
	// CORE
	// ----------------------------------------------------------

	private transient CVar DefaultRadarMode;
	private transient CVar SelectionMode;

	private transient CVar DrawOverHud;
	private transient CVar OffsetX;
	private transient CVar OffsetY;
	private transient CVar VisibilityFlags;
	private transient CVar Scale;
	private transient CVar Opacity;
	private transient CVar RadarColor;
	private transient CVar RangeUnits;
	private transient CVar RangeDivisions;

	protected transient CVar ScanDistance;
	protected transient CVar OverscanDistance;
	protected transient CVar AngleLimit;
	protected transient CVar ScanSpeed;

	private transient CVar ValidActors;

	private transient CVar RefreshSoundContact;
	private transient CVar RefreshSoundClear;
	private transient CVar RefreshSoundVolume;

	private transient CVar BatteryEnabled;
	private transient CVar BatteryDrainTime;
	private transient CVar BatteryRechargeTime;

	int RadarMode[MAXPLAYERS]; // [Ace] This should have been called RadarState.
	private int BatteryPower[MAXPLAYERS];
	private int AccumulatedDrain[MAXPLAYERS];

	private transient Array<RadarBlip> RadarBlips;
	private transient double ScanAngle;
	private transient int LastScanIndex;

	static void DestroyMainHandler()
	{
		let Handler = EventHandler.Find("ZRadarMainHandler");
		if (Handler)
		{
			Handler.Destroy();
		}
	}

	override void OnRegister()
	{
		DefaultRadarMode = CVar.GetCVar('zradar_defmode', players[consoleplayer]);
		SelectionMode = CVar.GetCVar('zradar_selectionmode', players[consoleplayer]);

		DrawOverHud = CVar.GetCVar('zradar_drawoverhud', players[consoleplayer]);
		OffsetX = CVar.GetCVar('zradar_offset_x', players[consoleplayer]);
		OffsetY = CVar.GetCVar('zradar_offset_y', players[consoleplayer]);
		Scale = CVar.GetCVar('zradar_scale', players[consoleplayer]);
		Opacity = CVar.GetCVar('zradar_opacity', players[consoleplayer]);
		RadarColor = CVar.GetCVar('zradar_color', players[consoleplayer]);
		RangeUnits = CVar.GetCVar('zradar_rangeunits', players[consoleplayer]);
		RangeDivisions = CVar.GetCVar('zradar_rangedivisions', players[consoleplayer]);
		VisibilityFlags = CVar.GetCVar('zradar_visibility', players[consoleplayer]);

		ScanDistance = CVar.GetCVar('zradar_distance');
		OverscanDistance = CVar.GetCVar('zradar_overdistance');
		AngleLimit = CVar.GetCVar('zradar_anglelimit');
		ScanSpeed = CVar.GetCVar('zradar_scanspeed');

		ValidActors = CVar.GetCVar('zradar_validactors');

		RefreshSoundContact = CVar.GetCVar('zradar_refreshsound_contact', players[consoleplayer]);
		RefreshSoundClear = CVar.GetCVar('zradar_refreshsound_clear', players[consoleplayer]);
		RefreshSoundVolume = CVar.GetCVar('zradar_refreshsoundvolume', players[consoleplayer]);
		
		BatteryEnabled = CVar.GetCVar('zradar_battery_enable');
		BatteryDrainTime = CVar.GetCVar('zradar_battery_draintime');
		BatteryRechargeTime = CVar.GetCVar('zradar_battery_rechargetime');
	}

	override void WorldLoaded(WorldEvent e)
	{
		BatteryPower[consoleplayer] = 100;
		SetRadarMode(consoleplayer, DefaultRadarMode.GetInt(), true);
	}

	override void WorldTick()
	{
		if (GameState != GS_LEVEL)
		{
			return;
		}

		int drainTime = int((35 * 60 * BatteryDrainTime.GetFloat()) / GetDrainMult(consoleplayer));
		int rechargeTime = int(35 * 60 * BatteryRechargeTime.GetFloat());
		let plr = players[consoleplayer].mo;

		if (!CheckRadarCondition(plr))
		{
			SetRadarMode(consoleplayer, Mode_Disabled);
			return;
		}

		// [Ace] Battery stuff.
		if (BatteryEnabled.GetBool())
		{
			// [Ace] Draining.
			if (RadarMode[consoleplayer] > Mode_Disabled)
			{
				BatteryPower[consoleplayer] = int((1.0 - AccumulatedDrain[consoleplayer]++ / double(drainTime)) * 100);

				if (BatteryPower[consoleplayer] <= 0)
				{
					SetRadarMode(consoleplayer, Mode_Disabled);
				}
			}

			// [Ace] Recharging.
			else if (RadarMode[consoleplayer] == Mode_Disabled && BatteryPower[consoleplayer] < 100)
			{
				BatteryPower[consoleplayer] = int((1.0 - AccumulatedDrain[consoleplayer]-- / double(rechargeTime)) * 100);

				if (plr && BatteryPower[consoleplayer] == 100)
				{
					plr.A_Print("\c[Cyan]Scanner fully charged!\c-", 2);
					plr.A_StartSound("Radar/GenAlert", 7, CHANF_UI | CHANF_LOCAL | CHANF_OVERLAP);
				}
			}
		}

		if (RadarMode[consoleplayer] > Mode_Disabled)
		{
			double speed = GetScanSpeed();

			// [Ace] Blip cleanup.
			for (int i = RadarBlips.Size() - 1; i >= 0; --i)
			{
				RadarBlip blip = RadarBlips[i];
				if (!blip)
				{
					RadarBlips.Delete(i);
					continue;
				}

				if (speed > 0 && blip.Scanned)
				{
					blip.Alpha -= speed / 360.0;
					if (blip.Alpha <= 0)
					{
						blip.Destroy();
						RadarBlips.Delete(i);
						continue;
					}
				}
			}

			int curScanIndex = int(abs(ceil(ScanAngle / 90.0)));
			if (speed == 0 && level.time % 35 == 0 || speed > 0 && (ScanAngle == 0 || LastScanIndex < curScanIndex))
			{
				// [Ace] Actively clear array because the blips will not fade away on their own.
				if (speed == 0)
				{
					RadarBlips.Clear();
				}
				else
				{
					LastScanIndex++;
				}

				int fullScanDist = max(ScanDistance.Getint(), OverscanDistance.GetInt());
				BlockThingsIterator it = BlockThingsIterator.Create(plr, fullScanDist);
				while (it.Next())
				{
					if (it.thing == players[consoleplayer].mo || it.thing.Health <= 0 || players[consoleplayer].mo.Distance2D(it.thing) - it.thing.Radius + it.thing.vel.length() > fullScanDist)
					{
						continue;
					}

					bool blipExists = false;
					for (int i = 0; i < RadarBlips.Size(); ++i)
					{
						if (RadarBlips[i].Other == it.thing && RadarBlips[i].Alpha > 0.25)
						{
							blipExists = true;
							break;
						}
					}

					if (blipExists)
					{
						continue;
					}

					RadarBlip blip = TryMakeBlip(RadarMode[consoleplayer], ValidActors.GetInt(), players[consoleplayer].mo, it.thing);
					if (blip)
					{
						RadarBlips.Push(blip);
					}
				}

				int refSoundContact = RefreshSoundContact.GetInt();
				int refSoundClear = RefreshSoundClear.GetInt();
				if (plr && RefSoundContact > 0 && (speed == 0 || curScanIndex % 4 == 0))
				{
					string snd = GetHostileBlipCount() > 0 ? "Radar/RefreshSound/Contact"..refSoundContact : "Radar/RefreshSound/Clear"..refSoundClear;
					plr.A_StartSound(snd, 7, CHANF_UI | CHANF_LOCAL | CHANF_OVERLAP, RefreshSoundVolume.GetFloat());
				}
			}

			if (speed > 0)
			{
				ScanAngle -= speed;
			}
		}
	}

	override void NetworkProcess(ConsoleEvent e)
	{
		if (e.Name ~== "ChangeRadarMode")
		{
			// [Ace] Force radar refresh when you switch modes.
			int selMode = SelectionMode.GetInt();
			
			// [Ace] Cycle modes.
			if (selMode == -1)
			{
				SetRadarMode(e.Player, (RadarMode[e.Player] + 1) % (Mode_Local + 1));
			}

			// [Ace] Or toggle between disabled and the selected mode.
			else
			{
				SetRadarMode(e.Player, RadarMode[e.Player] == selMode ? 0 : selMode);
			}
		}
	}

	private void SetRadarMode(int plrIndex, int mode, bool silent = false)
	{
		let plr = players[plrIndex].mo;
		if (!plr || plr.Health <= 0 || RadarMode[plrIndex] == Mode_Disabled && mode > Mode_Disabled && !CheckRadarCondition(plr) || RadarMode[plrIndex] == mode)
		{
			return;
		}

		RadarMode[plrIndex] = mode;

		if (!silent && plrIndex == consoleplayer)
		{
			string Message = "";
			switch (mode)
			{
				case Mode_Disabled: Message = "\c[Orange]Scanner Deactivated\c-"; break;
				case Mode_Full: Message = "\c[Green]Full Scanner Activated\c-"; break;
				case Mode_Local: Message = "\c[Red]Local Scanner Activated\c-"; break;	
			}

			plr.A_Print(Message, 1);
			plr.A_StartSound("Radar/GenAlert", 7, CHANF_UI | CHANF_LOCAL | CHANF_OVERLAP);
		}

		ScanAngle = 0;
		LastScanIndex = 0;
		RadarBlips.Clear();
	}

	protected virtual RadarBlip TryMakeBlip(int mode, int toggles, PlayerPawn plr, Actor other)
	{
		if (!plr || !other)
		{
			return null;
		}

		// [Ace] Pick the ones you want to check in each condition below.
		bool full = mode == Mode_Full;
		bool local = mode == Mode_Local && plr.CheckSight(other);

		if (full || local)
		{
			if (other.bISMONSTER)
			{
				if (toggles & 1 && !other.bFRIENDLY)
				{
					int SpawnHealth = other.GetSpawnHealth();
					if (SpawnHealth < 400)
					{
						return RadarBlip.Create(plr, other, "BLPDOT", RadarBlip.Col_Red, "BLPBELOW", "BLPABOVE");
					}
					else if (SpawnHealth < 1000)
					{
						return RadarBlip.Create(plr, other, "BLPDOT", RadarBlip.Col_Orange, "BLPBELOW", "BLPABOVE");
					}
					else
					{
						return RadarBlip.Create(plr, other, "BLPDOT", RadarBlip.Col_Gold, "BLPBELOW", "BLPABOVE");
					}
				}
				if (toggles & 1 << 1 && other.bFRIENDLY)
				{
					return RadarBlip.Create(plr, other, "BLPDOT", RadarBlip.Col_Cyan, "BLPBELOW", "BLPABOVE");
				}
			}
			if (toggles & 1 << 2 && other is "PlayerPawn" && other.player)
			{
				return RadarBlip.Create(plr, other, "BLPDOT", RadarBlip.Col_Green, "BLPBELOW", "BLPABOVE");
			}
		}
		if (full || local)
		{
			if (toggles & 1 << 3 && other is 'Health')
			{
				return RadarBlip.Create(plr, other, "BLPCROSS", RadarBlip.Col_Red, drawBox: (4, 4));
			}
			if (toggles & 1 << 4 && other is 'BasicArmorPickup')
			{
				return RadarBlip.Create(plr, other, "BLPCROSS", RadarBlip.Col_Blue, drawBox: (4, 4));
			}
			if (toggles & 1 << 5 && other is 'BasicArmorBonus')
			{
				return RadarBlip.Create(plr, other, "BLPCROSS", RadarBlip.Col_Green, drawBox: (3, 3));
			}
			if (toggles & 1 << 6 && other is 'Weapon')
			{
				return RadarBlip.Create(plr, other, "BLPSTAR", RadarBlip.Col_Gold);
			}
			if (toggles & 1 << 7 && other is 'Ammo')
			{
				return RadarBlip.Create(plr, other, "BLPSQARE", RadarBlip.Col_Tan, drawBox: (3, 3));
			}
			if (toggles & 1 << 8 && other is 'PowerupGiver')
			{
				return RadarBlip.Create(plr, other, "BLPSTAR", RadarBlip.Col_Pink);
			}
			if (toggles & 1 << 9 && other.bCOUNTITEM && !(other is 'BasicArmorBonus') && !(other is 'PowerupGiver'))
			{
				return RadarBlip.Create(plr, other, "BLPSQARE", RadarBlip.Col_Gold, drawBox: (3, 3));
			}
		}
		
		return null;
	}

	// ----------------------------------------------------------
	// UI
	// ----------------------------------------------------------

	const FontName = "ZRDRFONT";

	private transient ui HUDFont mMainFont;

	override void RenderUnderlay(RenderEvent e)
	{
		if (!DrawOverHud.GetBool())
		{
			DrawRadar(e.Camera);
		}
	}

	override void RenderOverlay(RenderEvent e)
	{
		if (DrawOverHud.GetBool())
		{
			DrawRadar(e.Camera);
		}
	}

	private ui void DrawRadar(Actor whomstve)
	{
		if (GameState != GS_LEVEL || AutomapActive || RadarMode[consoleplayer] == Mode_Disabled || whomstve != players[consoleplayer].mo) // [Ace] This should become paused > 0 when a new stable comes out.
		{
			return;
		}
		
		mMainFont = HUDFont.Create(FontName);

		let plr = PlayerPawn(whomstve);
		int OriginalWidth = StatusBar.HorizontalResolution;
		int OriginalHeight = StatusBar.VerticalResolution;

		double radarScale = Scale.GetFloat();

		int visFlags = VisibilityFlags.GetInt();

		double angleLimit = clamp(AngleLimit.GetInt(), 0, 360);
		if (angleLimit == 0)
		{
			angleLimit = plr.player.FOV;
		}

		int fullScanDist = max(ScanDistance.GetInt(), OverscanDistance.GetInt());
		int minScanDist = min(ScanDistance.GetInt(), OverscanDistance.GetInt());
		double speed = GetScanSpeed();

		StatusBar.BeginHUD(1.0, true);
		StatusBar.SetSize(StatusBar.RelTop, int(640 / radarScale), int(480 / radarScale));

		// --------------- SHARED ---------------

		double alpha = GetAlpha();

		// --------------- CIRCLE ---------------

		int rawCol = RadarColor.GetInt();
		if (rawCol == 0)
		{
			rawCol = plr.player.GetDisplayColor();
		}
		Color radCol = 0xFF000000 | rawCol;

		vector2 radarPos = (OffsetX.GetInt(), OffsetY.GetInt());
		int drawFlags = GetAlignmentFlags(radarPos);
		drawFlags |= StatusBar.DI_ITEM_CENTER;

		// [Ace] Main.
		if (visFlags & 1)
		{
			StatusBar.DrawImage("ZRMAIN", radarPos, drawFlags, alpha, col: radCol);
		}
		if (visFlags & 2)
		{
			StatusBar.DrawImage("ZRLINES", radarPos, drawFlags, alpha, col: radCol);
		}

		// [Ace] Scan circle.
		if (visFlags & 4 && speed > 0)
		{
			StatusBar.DrawImageRotated("ZRSCAN", radarPos, drawFlags, ScanAngle, alpha, col: radCol);
		}

		// [Ace] Range circles.
		int units = RangeUnits.GetInt();
		if (units > 0)
		{
			int divisions = RangeDivisions.GetInt();
			for (int i = 1; i <= divisions; ++i)
			{
				string str = "";
				switch (units)
				{
					case 1: str = StatusBar.FormatNumber(int(minScanDist / double(divisions) * i), 1, 4); break;
					case 2: str = StatusBar.FormatNumber(int(minScanDist / double(divisions) / 37.0 * i), 1, 3).."m"; break;
					case 3: str = StatusBar.FormatNumber(int(minScanDist / double(divisions) / 42.0 * i), 1, 3).."m"; break;
				}

				// [Ace] Circle.
				StatusBar.DrawImage("ZRRANGE", radarPos, drawFlags, alpha * 0.60, scale: (1.0, 1.0) * (i / double(divisions)), col: radCol);

				// [Ace] Number.
				StatusBar.DrawString(mMainFont, str, (radarPos.x - 1, radarPos.y + 2 - 60 / divisions * i), drawFlags | StatusBar.DI_TEXT_ALIGN_CENTER, Font.CR_WHITE, alpha * 0.60);
			}
		}

		// --------------- CROSS ---------------

		int CrossColor = 0;
		switch (RadarMode[plr.PlayerNumber()])
		{
			case Mode_Local: CrossColor = Font.CR_RED; break;
			case Mode_Full: CrossColor = Font.CR_GREEN; break;
		}
		StatusBar.DrawString(mMainFont, "+", (radarPos.x - 4, radarPos.y - 4), drawFlags, CrossColor, alpha);

		// --------------- ANGLE HANDS ---------------

		if (angleLimit > 0 && angleLimit < 360)
		{
			StatusBar.SetClipRect(radarPos.x - 60, radarPos.y - 60, 60, 120, drawFlags);
			StatusBar.DrawImageRotated("ZRHANDL", radarPos, drawFlags, angleLimit / 2, alpha, col: radCol);

			StatusBar.SetClipRect(radarPos.x, radarPos.y - 60, 60, 120, drawFlags);
			StatusBar.DrawImageRotated("ZRHANDR", radarPos, drawFlags, -angleLimit / 2, alpha, col: radCol);
			
			StatusBar.ClearClipRect();
		}

		// --------------- BLIPS ---------------

		for (int i = 0; i < RadarBlips.Size(); ++i)
		{
			RadarBlip blip = RadarBlips[i];
			if (!blip)
			{
				continue;
			}

			if (!blip.Scanned || speed == 0)
			{
				if (!blip.Other)
				{
					continue;
				}

				if (Actor.AbsAngle(plr.AngleTo(blip.Other), plr.angle) > angleLimit / 2 || speed > 0 && Actor.AbsAngle(plr.AngleTo(blip.Other), ScanAngle + plr.angle) > speed)
				{
					continue;
				}
				
				blip.Diff = (plr.pos.x - blip.Other.pos.x, plr.pos.y - blip.Other.pos.y, blip.Other.pos.z - plr.pos.z);
				blip.DrawPos = 
				(
					blip.Diff.x * cos(-plr.angle) - blip.Diff.y * sin(-plr.angle),
					blip.Diff.x * sin(-plr.angle) + blip.Diff.y * cos(-plr.angle)
				);
			}

			blip.Scanned = true;

			// [Ace] Directly nuke it if it goes out of scan range.
			double length = blip.DrawPos.Length();
			if (length > fullScanDist)
			{
				blip.Destroy();
				continue;
			}

			double outtaRangeFac = 1.0;
			if (length > minScanDist) // [Ace] aroo–∫as is a god. This is non-negotiable.
			{
				outtaRangeFac = minScanDist / length;
			}

			// [Ace] I don't know why, I don't want to know why, I shouldn't have to wonder why, but for whatever reason, the X and Y coordinates are flipped.
			// The magic number ensures the blips scale correctly with distance.
			// I think the number is supposed to be the radar's radius minus the blip's radius.
			vector2 finalPos = ((blip.DrawPos * outtaRangeFac) / (minScanDist / 58.60)) + (radarPos.y, radarPos.x);
			
			StatusBar.DrawImage(blip.GetIcon(), (finalPos.y, finalPos.x), drawFlags, alpha * blip.Alpha * (outtaRangeFac ** 3), blip.DrawBox, col: blip.DrawCol);
		}

		// --------------- PRETEND WE WERE NEVER HERE ---------------

		StatusBar.BeginHUD(1.0, false);
		StatusBar.SetSize(StatusBar.RelTop, OriginalWidth, OriginalHeight);
	}

	// ----------------------------------------------------------
	// INFORMATION
	// ----------------------------------------------------------

	// [Ace] This returns angle change per tic and not the raw revolutions per tic.
	protected clearscope double GetScanSpeed()
	{
		double speed = ScanSpeed.GetFloat();
		return speed * (360.0 / TICRATE);
	}

	protected virtual ui double GetAlpha()
	{
		double Alpha = Opacity.GetFloat();
		if (BatteryEnabled.GetBool())
		{
			double Mult = BatteryPower[consoleplayer] / 100.0;
			Alpha *= max(0.35, Mult);
		}
		return Alpha;
	}
	protected virtual clearscope bool CheckRadarCondition(PlayerPawn plr) { return true; }

	virtual double GetDrainMult(int playerIndex) const
	{
		switch (RadarMode[playerIndex])
		{
			case Mode_Local: return 0.5;
			default: return 1.0;
		}
	}

	private clearscope int GetHostileBlipCount()
	{
		int blips = 0;
		for (int i = 0; i < RadarBlips.Size(); ++i)
		{
			if (RadarBlips[i].IsHostile)
			{
				blips++;
			}
		}
		return blips;
	}

	private ui int GetAlignmentFlags(vector2 offsets) const
	{
		int AlignFlags = 0;
		if (offsets.x > 0)
		{
			AlignFlags |= StatusBar.DI_SCREEN_LEFT;
		}
		else if (offsets.x < 0)
		{
			AlignFlags |= StatusBar.DI_SCREEN_RIGHT;
		}
		else
		{
			AlignFlags |= StatusBar.DI_SCREEN_HCENTER;
		}

		if (offsets.y > 0)
		{
			AlignFlags |= StatusBar.DI_SCREEN_TOP;
		}
		else if (offsets.y < 0)
		{
			AlignFlags |= StatusBar.DI_SCREEN_BOTTOM;
		}
		else
		{
			AlignFlags |= StatusBar.DI_SCREEN_VCENTER;
		}
		
		return AlignFlags;
	}
}
