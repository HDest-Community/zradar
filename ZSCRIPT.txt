version 4.0

enum BatteryLevels
{
	Battery_Level_1 = 25,
	Battery_Level_2 = 75,
	Battery_Level_3 = 100,  // [Ace] Currently unused.
}

enum BlipType
{
	Blip_Hostile,
	Blip_PowerfulHostile,
	Blip_Friendly,
	Blip_Player,
	Blip_FragShard
}

enum UnitType
{
	Unit_MapUnit = 1,
	Unit_HDMeter
}

class RadarBlip
{
	double PlayerAngle;
	vector3 PlayerPos;
	vector3 TargetPos;
	BlipType Type;
}

class ZRadarMainHandler : EventHandler
{
	// ----------------------------------------------------------
	// CORE
	// ----------------------------------------------------------

	private transient CVar DefaultRadarMode;

	private transient CVar RadarPosition;
	private transient CVar RadarScale;
	private transient CVar RadarOpacity;
	private transient CVar RadarColor;
	private transient CVar RadarRangeUnits;
	private transient CVar RadarFrameTics;

	private transient CVar RadarDistance;
	private transient CVar RadarRefreshRate;

	private transient CVar BatteryEnabled;
	private transient CVar BatteryDrainRate;
	private transient CVar BatteryRechargeRate;

	private transient Array<RadarBlip> RadarBlips;
	private int RadarMode[8];
	private int BatteryPower[8];

	override void OnRegister()
	{
		DefaultRadarMode = CVar.GetCVar("zradar_defmode", players[consoleplayer]);

		RadarPosition = CVar.GetCVar("zradar_position", players[consoleplayer]);
		RadarScale = CVar.GetCVar("zradar_scale", players[consoleplayer]);
		RadarOpacity = CVar.GetCVar("zradar_opacity", players[consoleplayer]);
		RadarColor = CVar.GetCVar("zradar_color", players[consoleplayer]);
		RadarRangeUnits = CVar.GetCVar("zradar_rangeunits", players[consoleplayer]);
		RadarFrameTics = CVar.GetCVar("zradar_frametics", players[consoleplayer]);

		RadarDistance = CVar.GetCVar("zradar_distance", players[consoleplayer]);
		RadarRefreshRate = CVar.GetCVar("zradar_refreshrate", players[consoleplayer]);
		
		BatteryEnabled = CVar.GetCVar("zradar_battery_enable", players[consoleplayer]);
		BatteryDrainRate = CVar.GetCVar("zradar_battery_drain", players[consoleplayer]);
		BatteryRechargeRate = CVar.GetCVar("zradar_battery_recharge", players[consoleplayer]);

		BatteryPower[consoleplayer] = 100;
		RadarMode[consoleplayer] = BatteryEnabled.GetBool() ? GetRadarMode(BatteryPower[consoleplayer]) : DefaultRadarMode.GetInt();
	}

	private transient int DrainTics;
	private transient int RechargeTics;
	private transient int ArrayRefreshTics;

	override void WorldTick()
	{
		int DrainRate = BatteryDrainRate.GetInt();
		int RechargeRate = BatteryRechargeRate.GetInt();

		// [Ace] Battery stuff.
		if (BatteryEnabled.GetBool())
		{
			// [Ace] Draining.
			if (RadarMode[consoleplayer] > 0 && DrainTics++ == DrainRate)
			{
				BatteryPower[consoleplayer]--;
				RadarMode[consoleplayer] = GetRadarMode(BatteryPower[consoleplayer]);

				if (BatteryPower[consoleplayer] <= 0)
				{
					RadarMode[consoleplayer] = 0;
					let plr = players[consoleplayer].mo;
					if (plr)
					{
						plr.A_Print("\c[Orange]Scanner Deactivated");
						plr.A_StartSound("GenAlert", 7, CHANF_UI | CHANF_LOCAL | CHANF_OVERLAP);
					}
				}

				DrainTics = 0;
			}

			// [Ace] Recharging.
			else if (RadarMode[consoleplayer] == 0 && BatteryPower[consoleplayer] < 100 && RechargeTics++ == RechargeRate)
			{
				let plr = players[consoleplayer].mo;
				if (plr && ++BatteryPower[consoleplayer] == 100)
				{
					plr.A_Print("\c[Cyan]Scanner fully charged!");
					plr.A_StartSound("GenAlert", 7, CHANF_UI | CHANF_LOCAL | CHANF_OVERLAP);
				}

				RechargeTics = 0;
			}
		}

		// [Ace] This is where enemies are updated.
		if (RadarMode[consoleplayer] > 0 && ArrayRefreshTics++ >= RadarRefreshRate.GetInt())
		{
			RadarBlips.Clear();

			BlockThingsIterator it = BlockThingsIterator.Create(players[consoleplayer].mo, RadarDistance.GetInt());
			while (it.Next())
			{
				BlipType Type = ClassifyActor(it.Thing);
				if (Type == -1 || it.Thing == players[consoleplayer].mo || it.Thing.Health <= 0 || players[consoleplayer].mo.Distance2D(it.Thing) + it.Thing.Speed > RadarDistance.GetInt()) continue;

				if (RadarMode[consoleplayer] == 1 && players[consoleplayer].mo.CheckSight(it.Thing) || RadarMode[consoleplayer] == 2 && (it.Thing.vel.length() > 0 || it.Thing.target || it.Thing.master && it.Thing.master && it.Thing.master is "PlayerPawn") || RadarMode[consoleplayer] == 3 || Type >= Blip_Player)
				{
					let Blip = new("RadarBlip");
					Blip.Type = Type;
					Blip.TargetPos = it.Thing.pos;
					Blip.PlayerPos = players[consoleplayer].mo.pos;
					Blip.PlayerAngle = players[consoleplayer].mo.Angle;

					RadarBlips.Push(Blip);
				}
			}

			ArrayRefreshTics = 0;
		}
	}

	override void NetworkProcess(ConsoleEvent e)
	{
		if (e.Name ~== "ChangeRadarMode")
		{
			if (BatteryEnabled.GetBool())
			{
				RadarMode[e.Player] = RadarMode[e.Player] == 0 ? GetRadarMode(BatteryPower[e.Player]) : 0;
			}
			else
			{
				++RadarMode[e.Player] %= 4;
			}

			string Message = "";
			switch (RadarMode[e.Player])
			{
				case 0: Message = "\c[Orange]Scanner Deactivated"; break;
				case 1: Message = "\c[Red]Local Scanner Activated"; break;
				case 2: Message = "\c[Yellow]Movement Scanner Activated"; break;
				case 3: Message = "\c[Green]Full Scanner Activated"; break;
			}

			let plr = players[e.Player].mo;
			if (plr)
			{
				plr.A_Print(Message);
				plr.A_StartSound("GenAlert", 7, CHANF_UI | CHANF_LOCAL | CHANF_OVERLAP);
			}
		}
	}

	// ----------------------------------------------------------
	// UI
	// ----------------------------------------------------------

	const RadarFrames = 11;
	static const string RadarGraphics[] =
	{
		"R1SAFEZ0", "R1SAFEZ1", "R1SAFEZ2", "R1SAFEZ3", "R1SAFEZ4", "R1SAFEZ5", "R1SAFEZ6", "R1SAFEZ7", "R1SAFEZ8", "R1SAFEZ9", "R1SAFEZA",
		"R2SAFEZ0", "R2SAFEZ1", "R2SAFEZ2", "R2SAFEZ3", "R2SAFEZ4", "R2SAFEZ5", "R2SAFEZ6", "R2SAFEZ7", "R2SAFEZ8", "R2SAFEZ9", "R2SAFEZA"
	};

	const FontName = "ZRDRFONT";

	private transient ui HUDFont mMainFont;

	private transient ui int Frame;
	private transient ui int CircleUpdateTics;

	override void RenderUnderlay(RenderEvent e)
	{
		mMainFont = HUDFont.Create(FontName);

		int OriginalWidth = StatusBar.HorizontalResolution;
		int OriginalHeight = StatusBar.VerticalResolution;

		double OriginalAlpha = RadarOpacity.GetFloat();
		double Scale = RadarScale.GetFloat();
		int Position = RadarPosition.GetInt();
		int RefreshRate = RadarRefreshRate.GetInt();

		if (RadarMode[consoleplayer] > 0)
		{
			StatusBar.SetSize(StatusBar.RelTop, int(640 / Scale), int(480 / Scale));
			StatusBar.BeginHUD(1.0, true);

			// --------------- SHARED ---------------

			double Alpha = OriginalAlpha;
			if (BatteryEnabled.GetBool() && BatteryPower[consoleplayer] < Battery_Level_2)
			{
				double Mult = BatteryPower[consoleplayer] / double(Battery_Level_2);
				Alpha *= max(0.35, Mult);
			}

			// --------------- CIRCLE ---------------

			if (CircleUpdateTics++ >= RadarFrameTics.GetInt())
			{
				Frame++;
				Frame %= RadarFrames;

				CircleUpdateTics = 0;
			}

			vector2 RadarPos; int DrawFlags;
			[RadarPos, DrawFlags] = GetPositionData(Position);
			StatusBar.DrawImage(RadarGraphics[RadarFrames * RadarColor.GetInt() + Frame], RadarPos, DrawFlags, Alpha);

			int RangeColor = 0;
			switch (RadarColor.GetInt())
			{
				case 0: RangeColor = Font.CR_LIGHTBLUE; break;
				case 1: RangeColor = Font.CR_GREEN; break;
			}

			// [Ace] Range.
			int RangeUnits = RadarRangeUnits.GetInt();
			for (int i = 1; RangeUnits > 0 && i <= 3; ++i)
			{
				string RangeString = "";
				switch (RangeUnits)
				{
					case Unit_MapUnit: RangeString = StatusBar.FormatNumber(int(RadarDistance.GetInt() / 3.0 * i), 1, 4); break;
					case Unit_HDMeter: RangeString = StatusBar.FormatNumber(int(RadarDistance.GetInt() / 3.0 / 42.0 * i), 1, 3).."m"; break;
				}
				StatusBar.DrawString(mMainFont, RangeString, (RadarPos.x - 1, RadarPos.y + 2 - 20 * i), DrawFlags | BaseStatusBar.DI_TEXT_ALIGN_CENTER, RangeColor, Alpha * 0.75);
			}

			// --------------- CROSS ---------------

			int CrossColor = 0;
			switch (RadarMode[consoleplayer])
			{
				case 1: CrossColor = Font.CR_RED; break;
				case 2: CrossColor = Font.CR_YELLOW; break;
				case 3: CrossColor = Font.CR_GREEN; break;
			}
			StatusBar.DrawString(mMainFont, "+", (RadarPos.x - 4, RadarPos.y - 4), DrawFlags, CrossColor, Alpha);

			// --------------- BLIPS N STUFF ---------------

			Alpha *= RefreshRate > 1 ? 1.0 - ArrayRefreshTics / double(RefreshRate) : 1.0;

			for (int i = 0; i < RadarBlips.Size(); ++i)
			{
				double Scale = 17 * (RadarDistance.GetInt() / 1000.0);
				DrawBlip(StatusBar, RadarBlips[i], RadarPos, DrawFlags, Scale, Alpha);
			}

			// --------------- PRETEND WE WERE NEVER HERE ---------------

			StatusBar.SetSize(StatusBar.RelTop, OriginalWidth, OriginalHeight);
		}
	}

	private ui void DrawBlip(BaseStatusBar sbar, RadarBlip blip, vector2 drawPos, int flags, double scale, double alpha)
	{
		if (!blip) return;

		vector3 dif = (blip.PlayerPos.x - Blip.TargetPos.x, blip.PlayerPos.y - blip.TargetPos.y, blip.TargetPos.z - Blip.PlayerPos.z);

		// [Ace] This part of the code (the one with the maths) is the only one that doesn't directly belong to me. I don't know what the variables are supposed to mean because they use ye olde Ken Silverman naming conventions
		// and make fuck all sense to everyone else but the author. Name your damn variables properly! Anyway, I think the 'd' stands for "draw".
		double dx = dif.x * cos(-Blip.PlayerAngle) - dif.y * sin(-Blip.PlayerAngle);
		double dy = dif.x * sin(-Blip.PlayerAngle) + dif.y * cos(-Blip.PlayerAngle);
		dx /= scale; dx += drawPos.y;
		dy /= scale; dy += drawPos.x;

		string BlipImage = "";
		switch (blip.Type)
		{
			case Blip_Hostile:
				BlipImage = dif.z <= -60 ? "REDBELOW" : (dif.z >= 60 ? "REDABOVE" : "REDDOT");
				break;

			case Blip_PowerfulHostile:
				BlipImage = dif.z <= -60 ? "GLDBELOW" : (dif.z >= 60 ? "GLDABOVE" : "GLDDOT");
				break;

			case Blip_Friendly:
				BlipImage = dif.z <= -60 ? "CYNBELOW" : (dif.z >= 60 ? "CYNABOVE" : "CYNDOT");
				break;

			case Blip_Player:
				BlipImage = dif.z <= -60 ? "GRNBELOW" : (dif.z >= 60 ? "GRNABOVE" : "GRNDOT");
				break;

			case Blip_FragShard:
				BlipImage = "FRAGSHRD";
				break;
		}
		
		StatusBar.DrawImage(BlipImage, (dy, dx), flags, alpha);
	}

	// ----------------------------------------------------------
	// INFORMATION
	// ----------------------------------------------------------

	private BlipType ClassifyActor(Actor a) const
	{
		if (a.GetClassName() == 'BFGNecroShard') return Blip_FragShard;
		if (a is "PlayerPawn") return Blip_Player;
		if (a.bISMONSTER) return a.bFRIENDLY ? Blip_Friendly : (a.GetSpawnHealth() >= 1000 ? Blip_PowerfulHostile : Blip_Hostile);
		
		return -1;
	}

	private int GetRadarMode(int power) const
	{
		     if (power < Battery_Level_1) return 1;
		else if (power < Battery_Level_2) return 2;
		
		return 3;
	}

	private ui vector2, int GetPositionData(int position) const
	{
		vector2 pos; int flags;
		switch (position)
		{
			case 0:
				flags = BaseStatusBar.DI_SCREEN_LEFT_TOP | BaseStatusBar.DI_ITEM_CENTER;
				pos = (65, 65);
				break;
			case 1:
				flags = BaseStatusBar.DI_SCREEN_RIGHT_TOP | BaseStatusBar.DI_ITEM_CENTER;
				pos = (-65, 65);
				break;
			case 2:
				flags = BaseStatusBar.DI_SCREEN_LEFT_BOTTOM | BaseStatusBar.DI_ITEM_CENTER;
				pos = (65, -65);
				break;
			case 3:
				flags = BaseStatusBar.DI_SCREEN_RIGHT_BOTTOM | BaseStatusBar.DI_ITEM_CENTER;
				pos = (-65, -65);
				break;
		}

		return pos, flags;
	}
}