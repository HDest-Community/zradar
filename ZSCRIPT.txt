version "4.0"

enum BlipType
{
	Blip_Hostile,
	Blip_StrongHostile,
	Blip_PowerfulHostile,
	Blip_Friendly,
	Blip_Player,
	Blip_FragShard
}

enum RMode
{
	Mode_Disabled,
	Mode_Full,
	Mode_Movement,
	Mode_Local
}

enum UnitType
{
	Unit_MapUnit = 1,
	Unit_HDMeter
}

class RadarBlip
{
	double PlayerAngle;
	vector3 PlayerPos;
	vector3 TargetPos;
	BlipType Type;
}

class ZRadarMainHandler : EventHandler
{
	// ----------------------------------------------------------
	// CORE
	// ----------------------------------------------------------

	private transient CVar DefaultRadarMode;
	private transient CVar SelectionMode;

	private transient CVar DrawOverHud;
	private transient CVar Position;
	private transient CVar Scale;
	private transient CVar Opacity;
	private transient CVar RadarColor;
	private transient CVar RangeUnits;
	private transient CVar RangeDivisions;
	private transient CVar FrameTics;

	private transient CVar ScanDistance;
	private transient CVar AngleLimit;
	private transient CVar RefreshRate;

	private transient CVar RefreshSoundContact;
	private transient CVar RefreshSoundClear;
	private transient CVar RefreshSoundVolume;

	private transient CVar BatteryEnabled;
	private transient CVar BatteryDrainTime;
	private transient CVar BatteryRechargeTime;

	private transient Array<RadarBlip> RadarBlips;
	private int RadarMode[8]; // [Ace] This should have been called RadarState.
	private int BatteryPower[8];
	private int AccumulatedDrain[8];

	private transient int ArrayRefreshTics;

	static void DestroyMainHandler()
	{
		let Handler = EventHandler.Find("ZRadarMainHandler");
		Handler.Destroy();
	}

	override void OnRegister()
	{
		DefaultRadarMode = CVar.GetCVar("zradar_defmode", players[consoleplayer]);
		SelectionMode = CVar.GetCVar("zradar_selectionmode", players[consoleplayer]);

		DrawOverHud = CVar.GetCVar("zradar_drawoverhud", players[consoleplayer]);
		Position = CVar.GetCVar("zradar_position", players[consoleplayer]);
		Scale = CVar.GetCVar("zradar_scale", players[consoleplayer]);
		Opacity = CVar.GetCVar("zradar_opacity", players[consoleplayer]);
		RadarColor = CVar.GetCVar("zradar_color", players[consoleplayer]);
		RangeUnits = CVar.GetCVar("zradar_rangeunits", players[consoleplayer]);
		RangeDivisions = CVar.GetCVar("zradar_rangedivisions", players[consoleplayer]);
		FrameTics = CVar.GetCVar("zradar_frametics", players[consoleplayer]);

		ScanDistance = CVar.GetCVar("zradar_distance");
		AngleLimit = CVar.GetCVar("zradar_anglelimit");
		RefreshRate = CVar.GetCVar("zradar_refreshrate");

		RefreshSoundContact = CVar.GetCVar("zradar_refreshsound_contact", players[consoleplayer]);
		RefreshSoundClear = CVar.GetCVar("zradar_refreshsound_clear", players[consoleplayer]);
		RefreshSoundVolume = CVar.GetCVar("zradar_refreshsoundvolume", players[consoleplayer]);
		
		BatteryEnabled = CVar.GetCVar("zradar_battery_enable");
		BatteryDrainTime = CVar.GetCVar("zradar_battery_draintime");
		BatteryRechargeTime = CVar.GetCVar("zradar_battery_rechargetime");
	}

	override void WorldLoaded(WorldEvent e)
	{
		BatteryPower[consoleplayer] = 100;
		SetRadarMode(consoleplayer, DefaultRadarMode.GetInt(), true);
	}

	override void WorldTick()
	{
		if (GameState != GS_LEVEL) return;

		int DrainTime = int(35 * 60 * BatteryDrainTime.GetFloat() * GetDrainMult(RadarMode[consoleplayer]));
		int RechargeTime = int(35 * 60 * BatteryRechargeTime.GetFloat());
		int RefRate = RefreshRate.GetInt();
		let plr = players[consoleplayer].mo;

		if (!plr)
		{
			return;
		}

		if (!CheckRadarCondition(plr))
		{
			SetRadarMode(consoleplayer, Mode_Disabled);
			return;
		}

		// [Ace] Battery stuff.
		if (BatteryEnabled.GetBool())
		{
			// [Ace] Draining.
			if (RadarMode[consoleplayer] > Mode_Disabled)
			{
				BatteryPower[consoleplayer] = int((1.0 - AccumulatedDrain[consoleplayer]++ / double(DrainTime)) * 100);

				if (BatteryPower[consoleplayer] <= 0)
				{
					SetRadarMode(consoleplayer, Mode_Disabled);
				}
			}

			// [Ace] Recharging.
			else if (RadarMode[consoleplayer] == Mode_Disabled && BatteryPower[consoleplayer] < 100)
			{
				BatteryPower[consoleplayer] = int((1.0 - AccumulatedDrain[consoleplayer]-- / double(DrainTime)) * 100);

				if (plr && BatteryPower[consoleplayer] == 100)
				{
					plr.A_Print("\c[Cyan]Scanner fully charged!\c-", 2);
					plr.A_StartSound("Radar/GenAlert", 7, CHANF_UI | CHANF_LOCAL | CHANF_OVERLAP);
				}
			}
		}

		// [Ace] This is where enemies are updated.
		if (RadarMode[consoleplayer] > Mode_Disabled && ArrayRefreshTics++ >= RefRate)
		{
			RadarBlips.Clear();

			BlockThingsIterator it = BlockThingsIterator.Create(plr, ScanDistance.GetInt());
			while (it.Next())
			{
				BlipType Type = ClassifyActor(it.thing);
				if (Type == -1 || it.thing == players[consoleplayer].mo || it.thing.Health <= 0 || players[consoleplayer].mo.Distance2D(it.thing) - it.thing.Radius + it.thing.vel.length() > ScanDistance.GetInt())
				{
					continue;
				}

				int Angle = Actor.AbsAngle(plr.AngleTo(it.thing), plr.Angle);
				bool IsWithinScanAngle = Angle <= max(45, AngleLimit.GetInt()) / 2;

				// [Ace] I'm not too sure what e.Thing.target is for anymore. I know the master is checked for compatibility with Hideous Destructor's DERPs and HERPs.
				// I think target is so that it shows up if it's an enemy that's acquired a player?
				if (IsWithinScanAngle && (RadarMode[consoleplayer] == Mode_Local && players[consoleplayer].mo.CheckSight(it.thing) || RadarMode[consoleplayer] == Mode_Movement && (it.thing.vel.length() > 0 || it.thing.target || it.thing.master is "PlayerPawn") || RadarMode[consoleplayer] == Mode_Full || Type >= Blip_Player))
				{
					let Blip = new("RadarBlip");
					Blip.Type = Type;
					Blip.TargetPos = it.thing.pos;
					Blip.PlayerPos = players[consoleplayer].mo.pos;
					Blip.PlayerAngle = players[consoleplayer].mo.Angle;

					RadarBlips.Push(Blip);
				}
			}

			int RefSoundContact = RefreshSoundContact.GetInt();
			int RefSoundClear = RefreshSoundClear.GetInt();
			if (plr && RefRate >= 15 && RefSoundContact > 0) // [Ace] The > 15 check is so it doesn't screw up your hearing if you set it to something ridiculously low.
			{
				string snd = GetHostileBlipCount() > 0 ? "Radar/RefreshSound/Contact"..RefSoundContact : "Radar/RefreshSound/Clear"..RefSoundClear;
				plr.A_StartSound(snd, 7, CHANF_UI | CHANF_LOCAL | CHANF_OVERLAP, RefreshSoundVolume.GetFloat());
			}

			ArrayRefreshTics = 0;
		}
	}

	override void NetworkProcess(ConsoleEvent e)
	{
		if (e.Name ~== "ChangeRadarMode")
		{
			// [Ace] Force radar refresh when you switch modes.
			ArrayRefreshTics = RefreshRate.GetInt();
			int SelMode = SelectionMode.GetInt();
			
			// [Ace] Cycle modes.
			if (SelMode == -1)
			{
				SetRadarMode(e.Player, (RadarMode[e.Player] + 1) % 4);
			}

			// [Ace] Or toggle between disabled and the selected mode.
			else
			{
				SetRadarMode(e.Player, RadarMode[e.Player] == SelMode ? 0 : SelMode);
			}
		}
	}

	private void SetRadarMode(int plrIndex, int mode, bool silent = false)
	{
		let plr = players[plrIndex].mo;
		if (!plr || RadarMode[plrIndex] == Mode_Disabled && mode > Mode_Disabled && !CheckRadarCondition(plr) || RadarMode[plrIndex] == mode)
		{
			return;
		}

		RadarMode[plrIndex] = mode;

		if (!silent)
		{
			string Message = "";
			switch (mode)
			{
				case Mode_Disabled: Message = "\c[Orange]Scanner Deactivated\c-"; break;
				case Mode_Full: Message = "\c[Green]Full Scanner Activated\c-"; break;
				case Mode_Movement: Message = "\c[Yellow]Movement Scanner Activated\c-"; break;
				case Mode_Local: Message = "\c[Red]Local Scanner Activated\c-"; break;	
			}

			plr.A_Print(Message, 1);
			plr.A_StartSound("Radar/GenAlert", 7, CHANF_UI | CHANF_LOCAL | CHANF_OVERLAP);
		}
	}

	// ----------------------------------------------------------
	// UI
	// ----------------------------------------------------------

	const RadarFrames = 12;

	const FontName = "ZRDRFONT";

	private transient ui HUDFont mMainFont;

	private transient ui int Frame;
	private transient ui int CircleUpdateTics;

	override void RenderUnderlay(RenderEvent e)
	{
		if (!DrawOverHud.GetBool())
		{
			DrawRadar();
		}
	}

	override void RenderOverlay(RenderEvent e)
	{
		if (DrawOverHud.GetBool())
		{
			DrawRadar();
		}
	}

	private ui void DrawRadar()
	{
		if (GameState != GS_LEVEL || AutomapActive) return;
		
		mMainFont = HUDFont.Create(FontName);

		int OriginalWidth = StatusBar.HorizontalResolution;
		int OriginalHeight = StatusBar.VerticalResolution;

		double OriginalAlpha = Opacity.GetFloat();
		double RScale = Scale.GetFloat();
		int RPosition = Position.GetInt();
		int RefreshRate = RefreshRate.GetInt();

		if (RadarMode[consoleplayer] > Mode_Disabled)
		{
			StatusBar.BeginHUD(1.0, true);
			StatusBar.SetSize(StatusBar.RelTop, int(640 / RScale), int(480 / RScale));

			// --------------- SHARED ---------------

			double Alpha = OriginalAlpha;
			if (BatteryEnabled.GetBool())
			{
				double Mult = BatteryPower[consoleplayer] / 100.0;
				Alpha *= max(0.35, Mult);
			}

			// --------------- CIRCLE ---------------

			if (CircleUpdateTics++ >= FrameTics.GetInt())
			{
				Frame++;
				Frame %= RadarFrames + 1;

				CircleUpdateTics = 0;
			}

			int RadCol = RadarColor.GetInt();
			vector2 RadarPos; int DrawFlags;
			[RadarPos, DrawFlags] = GetPositionData(RPosition);

			// [Ace] Main.
			StatusBar.DrawImage("R"..RadCol.."MAIN", RadarPos, DrawFlags, Alpha);

			// [Ace] Scan circle.
			vector2 ScanScale = (1.0, 1.0);
			ScanScale *= Frame / double(RadarFrames);
			StatusBar.DrawImage("R"..RadCol.."SCAN", RadarPos, DrawFlags, Alpha, scale: ScanScale);

			// [Ace] Range circles.
			int RUnits = RangeUnits.GetInt();
			if (RUnits > 0)
			{
				int RangeColor = 0;
				switch (RadCol)
				{
					case 0: RangeColor = Font.CR_LIGHTBLUE; break;
					case 1: RangeColor = Font.CR_GREEN; break;
				}

				int RDiv = RangeDivisions.GetInt();
				for (int i = 1; i <= RDiv; ++i)
				{
					string RangeString = "";
					switch (RUnits)
					{
						case Unit_MapUnit: RangeString = StatusBar.FormatNumber(int(ScanDistance.GetInt() / double(RDiv) * i), 1, 4); break;
						case Unit_HDMeter: RangeString = StatusBar.FormatNumber(int(ScanDistance.GetInt() / double(RDiv) / 42.0 * i), 1, 3).."m"; break;
					}

					// [Ace] Circle.
					vector2 CircleScale = (1.0, 1.0);
					CircleScale *= i / double(RDiv);
					StatusBar.DrawImage("R"..RadCol.."RANGE", RadarPos, DrawFlags, Alpha * 0.60, scale: CircleScale);

					// [Ace] Number.
					StatusBar.DrawString(mMainFont, RangeString, (RadarPos.x - 1, RadarPos.y + 2 - 60 / RDiv * i), DrawFlags | BaseStatusBar.DI_TEXT_ALIGN_CENTER, RangeColor, Alpha * 0.60);
				}
			}

			// --------------- CROSS ---------------

			int CrossColor = 0;
			switch (RadarMode[consoleplayer])
			{
				case Mode_Local: CrossColor = Font.CR_RED; break;
				case Mode_Movement: CrossColor = Font.CR_YELLOW; break;
				case Mode_Full: CrossColor = Font.CR_GREEN; break;
			}
			StatusBar.DrawString(mMainFont, "+", (RadarPos.x - 4, RadarPos.y - 4), DrawFlags, CrossColor, Alpha);

			// --------------- BLIPS N STUFF ---------------

			Alpha *= RefreshRate > 1 ? 1.0 - ArrayRefreshTics / double(RefreshRate) : 1.0;

			for (int i = 0; i < RadarBlips.Size(); ++i)
			{
				double Scale = 17 * (ScanDistance.GetInt() / 1000.0);
				DrawBlip(StatusBar, RadarBlips[i], RadarPos, DrawFlags, Scale, Alpha);
			}

			// --------------- PRETEND WE WERE NEVER HERE ---------------

			StatusBar.BeginHUD(1.0, false);
			StatusBar.SetSize(StatusBar.RelTop, OriginalWidth, OriginalHeight);
		}
	}

	private ui void DrawBlip(BaseStatusBar sbar, RadarBlip blip, vector2 drawPos, int flags, double scale, double alpha)
	{
		if (!blip) return;

		vector3 dif = (blip.PlayerPos.x - Blip.TargetPos.x, blip.PlayerPos.y - blip.TargetPos.y, blip.TargetPos.z - Blip.PlayerPos.z);

		// [Ace] This part of the code (the one with the maths) is the only one that doesn't directly belong to me. I don't know what the variables are supposed to mean because they use ye olde Ken Silverman naming conventions
		// and make fuck all sense to everyone else but the author. Name your damn variables properly! Anyway, I think the 'd' stands for "draw".
		double dx = dif.x * cos(-Blip.PlayerAngle) - dif.y * sin(-Blip.PlayerAngle);
		double dy = dif.x * sin(-Blip.PlayerAngle) + dif.y * cos(-Blip.PlayerAngle);
		dx /= scale; dx += drawPos.y;
		dy /= scale; dy += drawPos.x;

		string BlipImage = "";
		switch (blip.Type)
		{
			case Blip_Hostile:
				BlipImage = dif.z <= -60 ? "REDBELOW" : (dif.z >= 60 ? "REDABOVE" : "REDDOT"); break;

			case Blip_StrongHostile:
				BlipImage = dif.z <= -60 ? "ORNBELOW" : (dif.z >= 60 ? "ORNABOVE" : "ORNDOT"); break;

			case Blip_PowerfulHostile:
				BlipImage = dif.z <= -60 ? "GLDBELOW" : (dif.z >= 60 ? "GLDABOVE" : "GLDDOT"); break;

			case Blip_Friendly:
				BlipImage = dif.z <= -60 ? "CYNBELOW" : (dif.z >= 60 ? "CYNABOVE" : "CYNDOT"); break;

			case Blip_Player:
				BlipImage = dif.z <= -60 ? "GRNBELOW" : (dif.z >= 60 ? "GRNABOVE" : "GRNDOT"); break;

			case Blip_FragShard:
				BlipImage = "FRAGSHRD"; break;
		}
		
		StatusBar.DrawImage(BlipImage, (dy, dx), flags, alpha);
	}

	// ----------------------------------------------------------
	// INFORMATION
	// ----------------------------------------------------------

	protected virtual bool CheckRadarCondition(PlayerPawn plr) { return true; }

	private double GetDrainMult(RMode mode) const
	{
		switch (mode)
		{
			case Mode_Local: return 2.0;
			case Mode_Movement: return 1.5;
			case Mode_Full: return 1.0;
		}

		return 1.0;
	}

	private BlipType ClassifyActor(Actor a) const
	{
		if (a.GetClassName() == 'BFGNecroShard') return Blip_FragShard;
		if (a is "PlayerPawn") return Blip_Player;
		if (a.bISMONSTER)
		{
			if (a.bFRIENDLY)
			{
				return Blip_Friendly;
			}
			else
			{
				int SpawnHealth = a.GetSpawnHealth();
				if (SpawnHealth < 400)
				{
					return Blip_Hostile;
				}
				else if (SpawnHealth < 1000)
				{
					return Blip_StrongHostile;
				}
				else
				{
					return Blip_PowerfulHostile;
				}
			}
		}
		
		return -1;
	}

	private int GetHostileBlipCount()
	{
		int HostileBlips = 0;

		for (int i = 0; i < RadarBlips.Size(); ++i)
		{
			if (RadarBlips[i].Type == Blip_Hostile || RadarBlips[i].Type == Blip_StrongHostile || RadarBlips[i].Type == Blip_PowerfulHostile)
			{
				HostileBlips++;
			}
		}

		return HostileBlips;
	}

	private ui vector2, int GetPositionData(int position)
	{
		vector2 pos; int flags;
		switch (position)
		{
			case 0:
				flags = BaseStatusBar.DI_SCREEN_LEFT_TOP | BaseStatusBar.DI_ITEM_CENTER;
				pos = (65, 65);
				break;
			case 1:
				flags = BaseStatusBar.DI_SCREEN_RIGHT_TOP | BaseStatusBar.DI_ITEM_CENTER;
				pos = (-65, 65);
				break;
			case 2:
				flags = BaseStatusBar.DI_SCREEN_LEFT_BOTTOM | BaseStatusBar.DI_ITEM_CENTER;
				pos = (65, -65);
				break;
			case 3:
				flags = BaseStatusBar.DI_SCREEN_RIGHT_BOTTOM | BaseStatusBar.DI_ITEM_CENTER;
				pos = (-65, -65);
				break;
		}

		return pos, flags;
	}
}
